import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'dev.architectury.loom' version '1.3-SNAPSHOT'

    // Vineflower is a fork of the FernFlower decompiler which contains many enhancements and bug fixes, and generally
    // produces much better source code. The Minecraft sources can be decompiled using it with the
    // "genSourcesWithVineflower" task.
    // (Note: Quiltflower was renamed to Vineflower in early July 2023.)
    id 'io.github.juuxel.loom-vineflower' version '1.11.0'

    // This dependency is only used to determine the state of the Git working tree so that build artifacts can be
    // more easily identified. TODO: Lazily load GrGit via a service only when builds are performed.
    id 'org.ajoberstar.grgit' version '5.0.0'
}

apply from: "${rootProject.projectDir}/gradle/forge.gradle"
apply from: "${rootProject.projectDir}/gradle/java.gradle"

archivesBaseName = "${project.archives_base_name}-mc${project.minecraft_version}"
version = "${project.mod_version}${getVersionMetadata()}"
group = project.maven_group

loom {
    mixin.defaultRefmapName = "mixins.sodium.refmap.json"
    accessWidenerPath = file("src/main/resources/sodium.accesswidener")
    mixin.defaultRefmapName = "mixins.rubidium.refmap.json"
	
	forge {
        convertAccessWideners = true
        mixinConfigs = [
                "rubidium.mixins.json",
                "rubidium.compat.mixins.json"
        ]
	}
}

configurations {
    modIncludeImplementation

    include.extendsFrom modIncludeImplementation
    modImplementation.extendsFrom modIncludeImplementation
}

sourceSets {
    api {
        java {
            compileClasspath += main.compileClasspath
        }
    }

    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }

    legacy {
        java {
            compileClasspath += main.compileClasspath
            compileClasspath += main.output
        }
    }
}

tasks.register('apiJar', Jar) {
    archiveClassifier.set "api-dev"

    from sourceSets.api.output
}

tasks.register('remapApiJar', RemapJarTask) {
    dependsOn apiJar
    archiveClassifier.set "api"

    input = apiJar.archiveFile.get().asFile
    addNestedDependencies = false
}

build.dependsOn apiJar
build.dependsOn remapApiJar

jar {
    from sourceSets.api.output.classesDirs
    from sourceSets.api.output.resourcesDir
    from sourceSets.legacy.output.classesDirs
    from sourceSets.legacy.output.resourcesDir
}

repositories {
    maven {
        url "https://maven.su5ed.dev/releases"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "dev.su5ed.sinytra.fabric-api:fabric-api:${project.fabric_version}"
}

def getVersionMetadata() {
    // CI builds only
    if (project.hasProperty("build.release")) {
        return "" // no tag whatsoever
    }

    if (grgit != null) {
        def head = grgit.head()
        def id = head.abbreviatedId

        // Flag the build if the build tree is not clean
        if (!grgit.status().clean) {
            id += "-dirty"
        }

        return "+git.${id}"
    }

    // No tracking information could be found about the build
    return "+unknown"
}
